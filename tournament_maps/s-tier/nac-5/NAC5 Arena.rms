
/* ****************   Palisade Arena for DE **********

created: 06 June 2014
updated: 04 January 2023
by Zetnus
zetnus@outlook.com
Zetnus#6414 on Discord

Learn about random map scripting: https://docs.google.com/document/d/e/2PACX-1vR_I1I9u-hI2FFm-EYyjoM_-9dNJFOfTaIgr05wXNbdpv9tI18b6r18ARULy3Jqyvlq6-lc2VjX6xP4/pub
*/

/* special 9-villager start version for NAC5 by Zetnus */

/* use two different different herdables so that you aren't giving all sheep 70 food */
#const HERDABLE_A 1245 /* Pig */
#const HERDABLE_B 594 /* Sheep */

/* placeholders used for positioning */
#const PH_NEUTRAL_OFF 346 /* Off-Grid Object - only used for gaia objects */
#const PH_PLAYER_OFF 647 /* Off-Grid Object - only used for player objects */

/* used to spawn 6 villagers in exact positions under the TC */
#const LAZY_VILLAGER 1285 /* FE Flag */

/* used to explore tiles around the TC */
#const EW_REVEALER 851 /* ES Flag */

start_random
	percent_chance 20
		#define ASIAN_MAP
	percent_chance 20
		#define ALPINE_MAP
end_random

#const GRASS_OTHER 16
#const UNDERBRUSH 71

if ALPINE_MAP
	#const BASE_FOREST 19	/* pine */	#const STRAGGLER_A 350
	#const FOREST_PATCH 19	/* pine */	#const STRAGGLER_B 350
	#const LAND_BASE_TILE 0	/* grass */
	#const CENTER_TERRAIN_TYPE 12	/* grass 2 */
	#const PRIMARY_LAND_PATCH 9 /* grass 3 */
	#const SECONDARY_LAND_PATCH 3 /* dirt 3 */
	#const TERTIARY_LAND_PATCH 0 /* grass */
elseif ASIAN_MAP
	#const BASE_FOREST 19	/* pine */	#const STRAGGLER_A 350
	#const FOREST_PATCH 18	/* bamboo */	#const STRAGGLER_B 348
	#const LAND_BASE_TILE 0	/* grass */
	#const CENTER_TERRAIN_TYPE 12	/* grass 2 */
	#const PRIMARY_LAND_PATCH 9 /* grass 3 */
	#const SECONDARY_LAND_PATCH 3 /* dirt 3 */
	#const TERTIARY_LAND_PATCH 0 /* grass */
else
	#const BASE_FOREST 10	/* oak */	#const STRAGGLER_A 349
	#const FOREST_PATCH 10	/* oak */	#const STRAGGLER_B 349
	#const LAND_BASE_TILE 9	/* grass 3 */
	#const CENTER_TERRAIN_TYPE 0	/* grass */
	#const PRIMARY_LAND_PATCH 6 /* dirt */
	#const SECONDARY_LAND_PATCH 9 /* grass 3 */
	#const TERTIARY_LAND_PATCH 3 /* dirt 3 */
endif


/* =================================================================================================== */
<PLAYER_SETUP>

random_placement
ai_info_map_type ARENA 0 0 0
behavior_version 2

/* creates the villagers from the flags under the TC */
effect_amount SET_ATTRIBUTE LAZY_VILLAGER ATTR_DEAD_ID 83
effect_amount SET_ATTRIBUTE LAZY_VILLAGER ATTR_HITPOINTS 0

/* adjust starting resources */
effect_amount MOD_RESOURCE AMOUNT_STARTING_WOOD ATTR_ADD -30
effect_amount MOD_RESOURCE AMOUNT_STARTING_FOOD ATTR_ADD -100

/* make the herdable under the TC die and only have 70 food */
/* note that you should NOT place this herdable type for gaia anywhere else */
effect_amount GAIA_ADD_ATTRIBUTE HERDABLE_A ATTR_STORAGE_VALUE -30	
effect_amount GAIA_SET_ATTRIBUTE HERDABLE_A ATTR_HITPOINTS 0

/* revealer line of sight */
effect_amount SET_ATTRIBUTE EW_REVEALER ATTR_LINE_OF_SIGHT 18
effect_amount SET_ATTRIBUTE EW_REVEALER ATTR_HITPOINTS 0

/* =================================================================================================== */
<LAND_GENERATION>

base_terrain BASE_FOREST

/* Fill in center */
create_land {              
	terrain_type CENTER_TERRAIN_TYPE
	land_percent 100
	left_border 18
	right_border 18
	top_border 18
	bottom_border 18
	border_fuzziness 30
	land_position 50 50
	zone 1
	if TINY_MAP	circle_radius 34 0	base_size 30
	elseif SMALL_MAP circle_radius 35 0	base_size 36
	elseif MEDIUM_MAP circle_radius 37 0	base_size 42
	elseif LARGE_MAP circle_radius 39 0	base_size 50
	elseif HUGE_MAP circle_radius 40 0	base_size 55
	elseif GIGANTIC_MAP circle_radius 40 0	base_size 60
	elseif LUDIKRIS_MAP circle_radius 40 0	base_size 120
	endif
	other_zone_avoidance_distance 0
}

create_player_lands {
	if LUDIKRIS_MAP base_size 25	number_of_tiles 5000
	else base_size 16	number_of_tiles 1600
	endif
	terrain_type LAND_BASE_TILE
	left_border 3 right_border 3 top_border 3 bottom_border 3
	border_fuzziness 25
	other_zone_avoidance_distance 15
}

/* =================================================================================================== */
<TERRAIN_GENERATION>

create_terrain FOREST_PATCH {
	base_terrain CENTER_TERRAIN_TYPE
	spacing_to_other_terrain_types 5
	land_percent 1
	number_of_clumps 3
	set_scale_by_groups
	set_avoid_player_start_areas 30
}

create_terrain LEAVES {
	base_terrain CENTER_TERRAIN_TYPE
	land_percent 8
	number_of_clumps 4
	set_scale_by_groups
	terrain_mask 1
}

create_terrain UNDERBRUSH {
	base_terrain CENTER_TERRAIN_TYPE
	land_percent 8
	number_of_clumps 4
	set_scale_by_groups
	terrain_mask 1
}

create_terrain PRIMARY_LAND_PATCH {
	base_terrain CENTER_TERRAIN_TYPE
	number_of_clumps 8
	land_percent 3
	set_scale_by_groups
	terrain_mask 1
}

create_terrain SECONDARY_LAND_PATCH {
	base_terrain CENTER_TERRAIN_TYPE
	number_of_clumps 24
	land_percent 1
	set_scale_by_groups
	terrain_mask 1
}

create_terrain TERTIARY_LAND_PATCH {
	base_terrain CENTER_TERRAIN_TYPE
	number_of_clumps 30
	land_percent 1
	set_scale_by_groups
	terrain_mask 1
}

create_terrain LEAVES {
	base_terrain LAND_BASE_TILE
	land_percent 2
	number_of_clumps 4
	set_scale_by_groups
	terrain_mask 1
}

create_terrain UNDERBRUSH {
	base_terrain LAND_BASE_TILE
	land_percent 2
	number_of_clumps 4
	set_scale_by_groups
	terrain_mask 1
}

create_terrain PRIMARY_LAND_PATCH {
	base_terrain LAND_BASE_TILE
	number_of_clumps 8
	land_percent 1
	set_scale_by_groups
	terrain_mask 1
}

create_terrain SECONDARY_LAND_PATCH {
	base_terrain LAND_BASE_TILE
	number_of_clumps 24
	land_percent 1
	set_scale_by_groups
	terrain_mask 1
}

create_terrain TERTIARY_LAND_PATCH {
	base_terrain LAND_BASE_TILE
	number_of_clumps 30
	land_percent 1
	set_scale_by_groups
	terrain_mask 1
}

/* =================================================================================================== */
<OBJECTS_GENERATION>

/* explore the area around the TC */
create_object EW_REVEALER
{
	set_place_for_every_player
	max_distance_to_players 0
}

create_object TOWN_CENTER {
	set_place_for_every_player 
	max_distance_to_players 0
}

#const WALL_TYPE 117	/* palisade wall */

create_object WALL_TYPE {
	set_place_for_every_player
	if LUDIKRIS_MAP
		min_distance_to_players 37
		max_distance_to_players 37
	else
		min_distance_to_players 21
		max_distance_to_players 21
	endif
}

create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

/* these actor areas are used to help position the villagers under the TC */
create_object PH_NEUTRAL_OFF /* Near Positioner */
{
	set_place_for_every_player
	set_gaia_object_only
	number_of_objects 5
	min_distance_to_players 1
	max_distance_to_players 1
	find_closest
	actor_area 5100
	actor_area_radius 0
}
create_object PH_NEUTRAL_OFF /* Villager Avoider */
{
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 1
	max_distance_to_players 1
	find_closest
	actor_area 5101
	actor_area_radius 0
	avoid_actor_area 5100 /* Near Positioner */
}
create_object PH_NEUTRAL_OFF /* Far Positioner */
{
	set_place_for_every_player
	set_gaia_object_only
	number_of_objects 5
	min_distance_to_players 2
	max_distance_to_players 2
	temp_min_distance_group_placement 1
	find_closest
	actor_area 5103
	actor_area_radius 1
}
create_object PH_NEUTRAL_OFF /* Blocking Seventh Villager */
{
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 2
	max_distance_to_players 2
	find_closest
	actor_area 5104
	actor_area_radius 0
	avoid_actor_area 5103 /* Far Positioner */
}


/* this is the sheep under the TC */
create_object PH_NEUTRAL_OFF
{
	set_place_for_every_player
	second_object HERDABLE_A
	set_gaia_object_only
	min_distance_to_players 1
	max_distance_to_players 1
	actor_area_radius 1
	avoid_actor_area 5100 /* Herdable Positioner */
	find_closest
	actor_area 20
}

/* the other 3 starting sheep */
create_object HERDABLE_A
{
	set_place_for_every_player
	number_of_objects 3
	set_loose_grouping
	group_placement_radius 2
	min_distance_to_players 5
	max_distance_to_players 6
	set_circular_placement
	actor_area 1113
	actor_area_radius 3
}

/* these are the sheep that are outside of the explored area - note that we only have 2 total */
/* this should NOT be the same herdable as is used for the decaying 70 food herdable under the TC */
create_object HERDABLE_B
{
	set_place_for_every_player
	set_gaia_object_only
	number_of_objects 2
	number_of_groups 1
	set_loose_grouping
	group_placement_radius 1
	min_distance_group_placement 2
	max_distance_to_other_zones 4
	avoid_forest_zone 2
	avoid_cliff_zone 2
	min_distance_to_players 19
	max_distance_to_players 20
}

/* this code is used to place 6 villagers under the TC in exactly optimal positions around the sheep */
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }
create_object PH_PLAYER_OFF { second_object LAZY_VILLAGER set_place_for_every_player min_distance_to_players 1 find_closest
	actor_area_to_place_in 20 actor_area 2 actor_area_radius 0 avoid_actor_area 2 avoid_actor_area 5101 avoid_actor_area 5104 }

/* this straggler will have 3 villagers near it */
create_object STRAGGLER_A
{
	set_place_for_every_player
	set_gaia_object_only
	find_closest
	actor_area 11
	actor_area_radius 1
	min_distance_to_players 5
	avoid_actor_area 1113
}

/* 3 villagers */
create_object VILLAGER
{
	number_of_objects 3
	set_place_for_every_player
	actor_area_to_place_in 11 /* Straggler */
	place_on_forest_zone
}

/* 2 starting houses */
create_object HOUSE
{
	set_place_for_every_player
	number_of_objects 2
	find_closest
	max_distance_to_other_zones 4
	avoid_forest_zone 2
	min_distance_to_players 8
	avoid_actor_area 1113
	min_distance_group_placement 3
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 11
		set_circular_placement
		min_distance_group_placement 2
		find_closest_to_map_center
	}
endif

if 2_PLAYER_GAME
create_object RELIC {
	number_of_objects 1
	min_distance_to_players 30
	temp_min_distance_group_placement 25
	min_distance_group_placement 1   
	terrain_to_place_on CENTER_TERRAIN_TYPE
	avoid_forest_zone 2
	set_place_for_every_player
	set_gaia_object_only
	find_closest
	actor_area 778
	actor_area_radius 22
	avoid_actor_area 778
}
create_object RELIC {
	number_of_objects 3
	min_distance_to_players 30
	temp_min_distance_group_placement 20
	min_distance_group_placement 1   
	terrain_to_place_on CENTER_TERRAIN_TYPE
	avoid_forest_zone 2
	avoid_actor_area 778
}
elseif LUDIKRIS_MAP
create_object RELIC {
	number_of_objects 250
	min_distance_to_players 40
	temp_min_distance_group_placement 110
	min_distance_group_placement 1
	terrain_to_place_on CENTER_TERRAIN_TYPE
	avoid_forest_zone 2
}
else
create_object RELIC {
	if TINY_MAP number_of_objects 5
	elseif SMALL_MAP number_of_objects 5
	elseif MEDIUM_MAP number_of_objects 5
	elseif LARGE_MAP number_of_objects 7
	else number_of_objects 8
	endif
	min_distance_to_players 23 
	temp_min_distance_group_placement 25
	min_distance_group_placement 1   
	terrain_to_place_on CENTER_TERRAIN_TYPE
	avoid_forest_zone 2
}
endif

/* near forage */
create_object FORAGE {
	number_of_objects 6
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 13
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 3
	actor_area 11
	actor_area_radius 11
}

/* near gold */
create_object GOLD {
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 16
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 6
	avoid_forest_zone 3
	actor_area 2001
	actor_area_radius 10
}

/* medium gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 17
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	avoid_actor_area 2001
	actor_area 2002
	actor_area_radius 10
}

/* far gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	max_distance_to_players 28
	terrain_to_place_on CENTER_TERRAIN_TYPE
	min_distance_group_placement 4
	avoid_forest_zone 2
	avoid_actor_area 2001
	avoid_actor_area 2002
	actor_area 2003
	actor_area_radius 10
}

/* near stone */
create_object STONE {
	number_of_objects 5
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 17
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	avoid_actor_area 2001
	avoid_actor_area 2002
	avoid_actor_area 2003
	actor_area 2004
	actor_area_radius 10
}

/* far stone */
create_object STONE {
	number_of_objects 4
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	max_distance_to_players 28
	terrain_to_place_on CENTER_TERRAIN_TYPE
	min_distance_group_placement 4
	avoid_forest_zone 2
	avoid_actor_area 2001
	avoid_actor_area 2002
	avoid_actor_area 2003
	avoid_actor_area 2004
	actor_area 2005
	actor_area_radius 10
}



create_object BOAR {
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_to_players 16
	max_distance_to_players 18
	set_circular_placement
	min_distance_group_placement 1
	temp_min_distance_group_placement 6
}

create_object DEER {
	start_random
		percent_chance 50	number_of_objects 4
		percent_chance 50	number_of_objects 3
	end_random
	number_of_groups 1
	group_placement_radius 3
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_to_players 16
	max_distance_to_players 18
	min_distance_group_placement 1
	avoid_actor_area 11 /* forage */
}

/* personal stragglers */
create_object STRAGGLER_A {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	set_circular_placement
	min_distance_group_placement 1
}
create_object STRAGGLER_B {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

create_object STRAGGLER_A {
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 8
	max_distance_to_players 19
	min_distance_group_placement 1
}

create_object STRAGGLER_B {
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 8
	max_distance_to_players 19
	min_distance_group_placement 1
}

/* scatter resources evenly across the map */

/* extra berries */
if 2_PLAYER_GAME
else
create_object FORAGE {
	number_of_objects 5
    number_of_groups 9320
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	temp_min_distance_group_placement 112
	min_distance_to_players 40
	terrain_to_place_on CENTER_TERRAIN_TYPE
}
endif

/* extra gold */
create_object GOLD {
	min_distance_to_players 40
	if TINY_MAP
			number_of_objects 3
			number_of_groups 2
			temp_min_distance_group_placement 30
	elseif SMALL_MAP
			number_of_objects 3
			number_of_groups 2
			temp_min_distance_group_placement 30
	elseif MEDIUM_MAP
			number_of_objects 3
			number_of_groups 3
			temp_min_distance_group_placement 30
	elseif LARGE_MAP
			number_of_objects 3
			number_of_groups 3
			temp_min_distance_group_placement 30
	elseif HUGE_MAP
			number_of_objects 4
			number_of_groups 4
			temp_min_distance_group_placement 30
	elseif GIGANTIC_MAP
			number_of_objects 4
			number_of_groups 5
			temp_min_distance_group_placement 30
	elseif LUDIKRIS_MAP
			number_of_objects 4
			number_of_groups 26
			temp_min_distance_group_placement 44
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	avoid_forest_zone 3
	terrain_to_place_on CENTER_TERRAIN_TYPE
}

/* extra stone */
create_object STONE {
	min_distance_to_players 40
	if TINY_MAP
			number_of_objects 2
			number_of_groups 2
			temp_min_distance_group_placement 30
	elseif SMALL_MAP
			number_of_objects 2
			number_of_groups 2
			temp_min_distance_group_placement 30
	elseif MEDIUM_MAP
			number_of_objects 3
			number_of_groups 2
			temp_min_distance_group_placement 30
	elseif LARGE_MAP
			number_of_objects 4
			number_of_groups 3
			temp_min_distance_group_placement 30
	elseif HUGE_MAP
			number_of_objects 4
			number_of_groups 4
			temp_min_distance_group_placement 30
	elseif GIGANTIC_MAP
			number_of_objects 4
			number_of_groups 9320
			temp_min_distance_group_placement 30
	elseif LUDIKRIS_MAP
			number_of_objects 4
			number_of_groups 9320
			temp_min_distance_group_placement 44
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	avoid_forest_zone 3
	terrain_to_place_on CENTER_TERRAIN_TYPE
}

create_object HAWK {
   number_of_objects  4
   set_scaling_to_map_size
}

create_object WOLF {
	number_of_objects 1
	set_scaling_to_player_number
	set_gaia_object_only
	min_distance_group_placement 1
	temp_min_distance_group_placement 12
	avoid_forest_zone 2
	min_distance_to_players 30
}

create_object WOLF {
	number_of_groups 1
	start_random
		percent_chance 75	number_of_objects 1
		percent_chance 25	number_of_objects 2
	end_random
	set_loose_grouping
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	temp_min_distance_group_placement 12
	avoid_forest_zone 2
	min_distance_to_players 40
} 

create_object STRAGGLER_A {
	number_of_objects 15
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players 23
}

create_object STRAGGLER_B {
	number_of_objects 15
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players 23
}



