/*
Shorelines
by Zetnus
zetnus@outlook.com
zetnus on Discord

Most recent update: 31 March 2025

Learn about random map scripting: https://tinyurl.com/7xteyu58
*/



#const UNDERBRUSH_JUNGLE 77
#const JUNGLEGRASS2 83
#const PALM_FOREST 112


/* =================================================================================================== */
<PLAYER_SETUP>

					effect_amount MOD_RESOURCE AMOUNT_STARTING_WOOD ATTR_ADD -30
					effect_amount MOD_RESOURCE AMOUNT_STARTING_FOOD ATTR_ADD -90
random_placement
behavior_version 2
ai_info_map_type CUSTOM 0 0 0


/* =================================================================================================== */
<LAND_GENERATION>

base_terrain DIRT2

create_player_lands {
	terrain_type DLC_JUNGLEGRASS
	land_percent 100
	base_size 12
	zone 1
	other_zone_avoidance_distance 11
	circle_radius  40 2
	base_elevation 1
}

/* the ocean */
create_land {
	terrain_type MED_WATER
	land_percent 10
	zone 2
	other_zone_avoidance_distance 9
	if 2_PLAYER_GAME
		assign_to_player 1
	else 
		assign_to AT_TEAM 0 0 0
	endif
	land_id 20
}


if 2_PLAYER_GAME
/* fake land for player spacing */
create_land {
	terrain_type GRASS
	land_percent 0
	base_size 2
	zone 1
	other_zone_avoidance_distance 11
	assign_to_player 2
	land_id 21
	base_elevation 1
}
endif

/* island */
create_land {
	terrain_type JUNGLEGRASS2
	land_percent 100
	if MAPSIZE_MINI	base_size 5
	elseif MAPSIZE_TINY	base_size 10
	elseif MAPSIZE_SMALL	base_size 15
	elseif MAPSIZE_MEDIUM	base_size 20
	elseif MAPSIZE_NORMAL	base_size 25
	elseif MAPSIZE_LARGE	base_size 30
	elseif MAPSIZE_HUGE	base_size 35
	elseif MAPSIZE_GIANT	base_size 45
	elseif MAPSIZE_MASSIVE	base_size 50
	elseif MAPSIZE_ENORMOUS	base_size 55
	elseif MAPSIZE_COLOSSAL	base_size 60
	elseif MAPSIZE_INCREDIBLE	base_size 65
	elseif MAPSIZE_MONSTROUS	base_size 70
	elseif MAPSIZE_LUDICROUS	base_size 75
	endif
	zone 3
	land_id 3
	other_zone_avoidance_distance 11
	land_position  50 50
	left_border 25	right_border 25	top_border 25	bottom_border 25
	base_elevation 1
}

/* =================================================================================================== */
<ELEVATION_GENERATION>


create_elevation 4 {
	base_terrain DLC_JUNGLEGRASS
	set_scale_by_size
	number_of_tiles 1000
	number_of_clumps 25
	enable_balanced_elevation
}

create_elevation 2 {
	base_terrain JUNGLEGRASS2
	set_scale_by_size
	number_of_tiles 400
	number_of_clumps 2
	enable_balanced_elevation
}


/* =================================================================================================== */
<TERRAIN_GENERATION>

color_correction CC_JUNGLE

create_terrain MED_WATER { base_terrain DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 terrain_mask 2 }
create_terrain MED_WATER { base_terrain DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 terrain_mask 2 }
create_terrain MED_WATER { base_terrain DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 terrain_mask 2 }
create_terrain MED_WATER { base_terrain DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 terrain_mask 2 }
create_terrain MED_WATER { base_terrain DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 terrain_mask 2 }

create_terrain DLC_WATER5 { base_terrain MED_WATER base_layer DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only }
create_terrain DLC_WATER5 { base_terrain MED_WATER base_layer DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only }
create_terrain DLC_WATER5 { base_terrain MED_WATER base_layer DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only }
create_terrain DLC_WATER5 { base_terrain MED_WATER base_layer DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only }
create_terrain DLC_WATER5 { base_terrain MED_WATER base_layer DIRT2 land_percent 100 number_of_clumps 9320 height_limits 0 0 set_flat_terrain_only }


/* woodlines */
create_terrain DLC_RAINFOREST {
	base_terrain DLC_JUNGLEGRASS
	land_percent 7
	number_of_clumps 18
	set_scale_by_groups
	set_avoid_player_start_areas 15
	spacing_to_other_terrain_types 4
}

create_terrain DLC_RAINFOREST {
	base_terrain GRASS
	land_percent 100
	number_of_clumps 18
}

create_terrain PALM_FOREST {
	base_terrain JUNGLEGRASS2
	land_percent 2
	number_of_clumps 5
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 3
}

/*
create_terrain PALM_FOREST {
	base_terrain JUNGLEGRASS2
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 7
}
*/

/* terrain mixing */
create_terrain DLC_DRYGRASS {
	base_terrain DLC_JUNGLEGRASS
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
	height_limits 2 7
}
create_terrain DLC_DRYGRASS {
	base_terrain DLC_RAINFOREST
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
	height_limits 2 7
}
create_terrain UNDERBRUSH_JUNGLE {
	base_terrain  DLC_JUNGLEGRASS
	land_percent 9
	number_of_clumps 20
	set_scale_by_groups
	terrain_mask 1
}
create_terrain GRASS2 {
	base_terrain  DLC_JUNGLEGRASS
	land_percent 9
	number_of_clumps 10
	set_scale_by_groups
	terrain_mask 1
}


create_terrain DLC_DRYGRASS {
	base_terrain JUNGLEGRASS2
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
	height_limits 2 7
}
create_terrain DLC_DRYGRASS {
	base_terrain JUNGLEGRASS2
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
	height_limits 2 7
}
create_terrain UNDERBRUSH_JUNGLE {
	base_terrain  JUNGLEGRASS2
	land_percent 2
	number_of_clumps 5
	set_scale_by_groups
	terrain_mask 1
}
create_terrain GRASS2 {
	base_terrain  JUNGLEGRASS2
	land_percent 2
	number_of_clumps 5
	set_scale_by_groups
	terrain_mask 1
}


/* water mixing */
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 3
	terrain_mask 1
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 4
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
	terrain_mask 1
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 4
}	

create_terrain WATER {
	base_terrain DLC_WATER5
	land_percent 100
	number_of_clumps 9320
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain WATER {
	base_terrain DLC_WATER5
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
}

create_terrain MED_WATER {
	base_terrain DLC_WATER5
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 1
	terrain_mask 2
}



/* =================================================================================================== */
<CONNECTION_GENERATION>

create_connect_to_nonplayer_land {
	replace_terrain WATER SHALLOW
	replace_terrain DLC_WATER5 SHALLOW
	replace_terrain PALM_FOREST JUNGLEGRASS2

	terrain_cost MED_WATER 0
	terrain_cost DEEP_WATER 0
	terrain_cost DLC_WATER4 0
	
	terrain_size DLC_WATER5 6 0
	terrain_size WATER 6 0
	terrain_size PALM_FOREST 5 0
}	


/* =================================================================================================== */
<OBJECTS_GENERATION>



		#const ESCALATION_REVEALER 1775
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_LINE_OF_SIGHT 20
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_HITPOINTS 0

				create_object ESCALATION_REVEALER
				{
					set_place_for_every_player
					min_distance_to_players 0
					max_distance_to_players 0
				}
/* player objects */
create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}

		/* Placeholder object with no terrain restrictions. */
		#const PH_GENERIC_ON 600
		#const PH_GENERIC_OFF 649
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_STORAGE_VALUE 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_STORAGE_VALUE 0
	
		/* Placeholder object that may only generate on land terrains. */
		#const PH_LAND_ON 601
		#const PH_LAND_OFF 643
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_TERRAIN_ID 8
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_STORAGE_VALUE 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_TERRAIN_ID 8
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_STORAGE_VALUE 0
	
		#const LAZY_MALE 1285 /* FE Flag */
		effect_amount SET_ATTRIBUTE LAZY_MALE ATTR_DEAD_ID 83
		effect_amount SET_ATTRIBUTE LAZY_MALE ATTR_HITPOINTS 0
	
		#const LAZY_FEMALE 851 /* ES Flag */
		effect_amount SET_ATTRIBUTE LAZY_FEMALE ATTR_DEAD_ID 293
		effect_amount SET_ATTRIBUTE LAZY_FEMALE ATTR_HITPOINTS 0

			
				create_object PH_GENERIC_OFF
				{
					set_place_for_every_player
					set_gaia_object_only
					min_distance_to_players 0
					max_distance_to_players 0
			
					actor_area 6001
					actor_area_radius 1
				}

					create_object PH_GENERIC_OFF /* Near Positioner */
					{
						set_place_for_every_player
						set_gaia_object_only
						number_of_objects 5
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 5100
						actor_area_radius 0

						if DEBUG_MODE
							second_object FLAG_A
						endif
					}

					create_object PH_GENERIC_OFF /* Far Positioner */
					{
						set_place_for_every_player
						set_gaia_object_only
						number_of_objects 5
						max_distance_to_players 2
						temp_min_distance_group_placement 1
						find_closest

						actor_area 5101
						actor_area_radius 1

						avoid_actor_area 6001 /* Minimum Player Distance */

						if DEBUG_MODE
							second_object FLAG_B
						endif
					}

					create_object PH_GENERIC_OFF /* Villager Avoider */
					{
						set_place_for_every_player
						set_gaia_object_only
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 5102
						actor_area_radius 0

						avoid_actor_area 5100 /* Near Positioner */

						if DEBUG_MODE
							second_object FLAG_C
						endif
					}

					create_object PH_GENERIC_OFF /* Blocking Seventh Villager */
					{
						set_place_for_every_player
						set_gaia_object_only
						max_distance_to_players 2
						find_closest

						actor_area 5103
						actor_area_radius 0

						avoid_actor_area 5101 /* Far Positioner */
						avoid_actor_area 6001 /* Minimum Player Distance */

						if DEBUG_MODE
							second_object FLAG_D
						endif
					}

					if CENTRALISE_OBJECT
						create_object PH_GENERIC_ON
					else
						create_object PH_GENERIC_OFF
					endif
					{
						second_object DLC_WATERBUFFALO
						set_place_for_every_player
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 2000
						actor_area_radius 1

						avoid_actor_area 5100 /* Herdable Positioner */
					}

				start_random
				  percent_chance 50 #define LM_A
				  percent_chance 50 #define LF_A
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_A
				   create_object LAZY_MALE
				  elseif LF_A
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_A
				     second_object LAZY_MALE
				    elseif LF_A
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_B
				  percent_chance 50 #define LF_B
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_B
				   create_object LAZY_MALE
				  elseif LF_B
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_B
				     second_object LAZY_MALE
				    elseif LF_B
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_C
				  percent_chance 50 #define LF_C
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_C
				   create_object LAZY_MALE
				  elseif LF_C
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_C
				     second_object LAZY_MALE
				    elseif LF_C
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_D
				  percent_chance 50 #define LF_D
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_D
				   create_object LAZY_MALE
				  elseif LF_D
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_D
				     second_object LAZY_MALE
				    elseif LF_D
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_E
				  percent_chance 50 #define LF_E
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_E
				   create_object LAZY_MALE
				  elseif LF_E
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_E
				     second_object LAZY_MALE
				    elseif LF_E
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_F
				  percent_chance 50 #define LF_F
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_F
				   create_object LAZY_MALE
				  elseif LF_F
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_F
				     second_object LAZY_MALE
				    elseif LF_F
				      second_object LAZY_FEMALE
				    endif
				  endif
				}

create_object PALMTREE
{ 
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 5
	max_distance_to_players 6
	set_circular_placement
	enable_tile_shuffling
	avoid_forest_zone 2
	avoid_actor_area 9
	number_of_objects 1
	actor_area 5423
	actor_area_radius 1
}

create_object VILLAGER
{
	set_place_for_every_player
	number_of_objects 3
	actor_area_to_place_in 5423
}

create_object HOUSE 
{
	number_of_objects 2
	temp_min_distance_group_placement 6
	find_closest
	set_place_for_every_player
	min_distance_to_players 8
}


create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}



if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 11
		set_circular_placement
		min_distance_group_placement 2
	}
endif

/* default straggler trees */
if DEATH_MATCH
elseif DEFEND_WONDER
elseif EMPIRE_WARS
else
create_object PALMTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	set_circular_placement
	min_distance_group_placement 1
}
create_object PALMTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}
endif

create_object DLC_ORANGEBUSH
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 11
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	max_distance_to_other_zones 12
}

create_object DLC_WATERBUFFALO {
	number_of_objects 2
	set_loose_grouping
	set_place_for_every_player
	min_distance_to_players 8
	find_closest
	set_circular_placement
	avoid_forest_zone 1
}

create_object GOLD {
	number_of_objects 5
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 14
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
}

create_object STONE {
	number_of_objects 3
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 14
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
}

create_object STONE {
	number_of_objects 3
	number_of_groups 2
	temp_min_distance_group_placement 35
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
}

create_object GOLD {
	number_of_objects 5
	number_of_groups 2
	temp_min_distance_group_placement 35
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
}

create_object BOAR {
	number_of_objects 1
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 15
	avoid_forest_zone 2
	max_distance_to_other_zones 4
	min_distance_to_map_edge 2
	terrain_to_place_on DLC_JUNGLEGRASS
}


create_object DLC_WATERBUFFALO {
	number_of_objects 1
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 15
	avoid_forest_zone 2
	max_distance_to_other_zones 4
	min_distance_to_map_edge 2
}


create_object GAZELLE {
	number_of_objects 3
	number_of_groups 1
	group_placement_radius 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	max_distance_to_other_zones 4
	min_distance_to_map_edge 2
}

if 2_PLAYER_GAME
/* relics by the water */
create_object RELIC {
	number_of_objects 2
	place_on_specific_land_id 20
	set_gaia_object_only
	avoid_forest_zone 1
	max_distance_to_other_zones 3
	min_distance_group_placement 1
	min_distance_to_players 23
	set_circular_placement
	find_closest
	temp_min_distance_group_placement 60
	terrain_to_place_on DLC_JUNGLEGRASS
	ignore_terrain_restrictions 
	actor_area 30
	actor_area_radius 30
	avoid_actor_area 30
}
else
/* relics per player */
create_object RELIC {
	number_of_objects 1
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 33
	find_closest
	set_circular_placement
	min_distance_group_placement 1
	avoid_forest_zone 2
	max_distance_to_other_zones 4
	min_distance_to_map_edge 2
	actor_area 30
	actor_area_radius 30
	avoid_actor_area 30
}
endif




/* fake player land */

create_object RELIC {
	number_of_objects 2
	place_on_specific_land_id 21
	set_gaia_object_only
	avoid_forest_zone 1
	max_distance_to_other_zones 3
	min_distance_group_placement 1
	min_distance_to_players 20
	set_circular_placement
	find_closest
	temp_min_distance_group_placement 30
	terrain_to_place_on DLC_JUNGLEGRASS
	actor_area 30
	actor_area_radius 30
	avoid_actor_area 30
}



create_object GOLD {
	place_on_specific_land_id 21
	set_gaia_object_only
	number_of_objects 5
	number_of_groups 2
	temp_min_distance_group_placement 30
	group_placement_radius 2
	set_tight_grouping
	min_distance_to_players 25
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
	terrain_to_place_on DLC_JUNGLEGRASS
}

create_object STONE {
	place_on_specific_land_id 21
	set_gaia_object_only
	number_of_objects 3
	number_of_groups 2
	temp_min_distance_group_placement 32
	group_placement_radius 2
	set_tight_grouping
	min_distance_to_players 27
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
	terrain_to_place_on DLC_JUNGLEGRASS
}

create_object GOLD {
	place_on_specific_land_id 21
	set_gaia_object_only
	number_of_objects rnd(3,5)
	number_of_groups 1
	group_placement_radius 3
	set_tight_grouping
	min_distance_to_players 3
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	max_distance_to_other_zones 5
	min_distance_to_map_edge 2
	terrain_to_place_on DLC_JUNGLEGRASS
}


/* center */
create_object RELIC {
	if 3_PLAYER_GAME number_of_objects 2 endif
	place_on_specific_land_id 3 
	avoid_forest_zone 5
	max_distance_to_other_zones 3
	min_distance_group_placement 1
	min_distance_to_players 5
	set_circular_placement
	find_closest
	terrain_to_place_on JUNGLEGRASS2
	actor_area 30
	actor_area_radius 30
	avoid_actor_area 30
}


create_object DLC_ORANGEBUSH {
	number_of_objects 4
	number_of_groups 6
	set_scaling_to_map_size
	temp_min_distance_group_placement 4
	place_on_specific_land_id 3 
	avoid_forest_zone 1
	max_distance_to_other_zones 5
	min_distance_group_placement 1
	set_circular_placement
	find_closest
	max_distance_to_players 12
	terrain_to_place_on JUNGLEGRASS2
}

create_object GAZELLE {
	number_of_objects 4
	set_scaling_to_map_size
	temp_min_distance_group_placement 2
	place_on_specific_land_id 3 
	avoid_forest_zone 2
	max_distance_to_other_zones 2
	min_distance_group_placement 1
	set_circular_placement
	find_closest
	max_distance_to_players 12
	terrain_to_place_on JUNGLEGRASS2
}

create_object GOLD {
	number_of_objects rnd(2,3)
	number_of_groups 6
	set_tight_grouping
	set_scaling_to_map_size
	temp_min_distance_group_placement 12
	place_on_specific_land_id 3 
	avoid_forest_zone 2
	max_distance_to_other_zones 3
	min_distance_group_placement 1
	set_circular_placement
	find_closest
	min_distance_to_players 14
	terrain_to_place_on JUNGLEGRASS2
}

create_object PALMTREE {
	number_of_groups 15
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 7
	min_distance_to_players 22
	set_circular_placement
}


/* fish */

create_object OYSTERS {
	number_of_objects 9320
	set_gaia_object_only
	min_distance_group_placement  3
	temp_min_distance_group_placement 12
	terrain_to_place_on DLC_WATER5
	max_distance_to_other_zones 7
}


create_object SHORE_FISH {
	number_of_objects 9320
	set_gaia_object_only
	min_distance_group_placement  3
	temp_min_distance_group_placement 6
}

create_object MARLIN1 {
	number_of_objects 9320
	set_gaia_object_only
	min_distance_group_placement 4 
	temp_min_distance_group_placement 12
	max_distance_to_other_zones 7
	terrain_to_place_on DLC_WATER4
	avoid_actor_area 10
} 

create_object DORADO {
	number_of_objects 9320
	set_gaia_object_only
	max_distance_to_other_zones 2
	min_distance_group_placement 1
	temp_min_distance_group_placement 10
	avoid_actor_area 10
}

/* birds */
create_object MACAW {
	number_of_objects 6
	set_scaling_to_map_size
}
