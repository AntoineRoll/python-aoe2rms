/*
Stone Rush
by Zetnus
zetnus@outlook.com
zetnus on Discord

Most recent update: 31 March 2025

Learn about random map scripting: https://tinyurl.com/7xteyu58
*/

#const OAK_BUSH_FOREST 20
#const GRAVEL_DEFAULT 70
#const GRAVEL_ROAD 78
#const GRAVEL_WET 81
#const ROCK_WET 82
#const BUSH_FOREST 89

#const ONGRID_PLACEHOLDER 278	/* dead fish trap */
#const STONE_ROCK 839 
#const FALCON 1056


/* =================================================================================================== */
<PLAYER_SETUP>
					effect_amount MOD_RESOURCE AMOUNT_STARTING_WOOD ATTR_ADD -30
					effect_amount MOD_RESOURCE AMOUNT_STARTING_FOOD ATTR_ADD -90

random_placement
behavior_version 2
ai_info_map_type CUSTOM 0 0 0


/* =================================================================================================== */
<LAND_GENERATION>

base_terrain PINE_FOREST

create_player_lands {
	land_percent 100
	terrain_type GRASS2
	base_size 14
	circle_radius 26 0
	left_border 12
	right_border 12
	top_border 12
	bottom_border 12
	border_fuzziness 20
	zone 1
	other_zone_avoidance_distance 0
	base_elevation 12
}

create_land {
	base_size 9
	land_percent 100
	terrain_type GRAVEL_WET
	land_position 50 50
	left_border 38
	right_border 38
	top_border 38
	bottom_border 38
	zone 1
	other_zone_avoidance_distance 0
	land_id 1
}

/* =================================================================================================== */
<ELEVATION_GENERATION>


/* =================================================================================================== */
<TERRAIN_GENERATION>

create_terrain GRASS { base_terrain GRASS2 terrain_mask 1 height_limits 9 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups  }
create_terrain GRASS { base_terrain GRASS2 terrain_mask 0 height_limits 10 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRASS3 { base_terrain GRASS terrain_mask 1 height_limits 11 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRASS3 { base_terrain GRASS terrain_mask 0 height_limits 12 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain DLC_DRYGRASS { base_terrain GRASS3 terrain_mask 1 height_limits 8 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain DLC_DRYGRASS { base_terrain GRASS3 terrain_mask 0 height_limits 10 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain DIRT2 { base_terrain DLC_DRYGRASS terrain_mask 1 height_limits 11 11 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRAVEL_DEFAULT { base_terrain DLC_DRYGRASS terrain_mask 1 height_limits 11 12 land_percent 100 number_of_clumps 9320 set_scale_by_groups }

create_terrain GRASS { base_terrain GRAVEL_WET terrain_mask 1 height_limits 8 8 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRASS3 { base_terrain GRAVEL_WET terrain_mask 1 height_limits 7 7 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRAVEL_DEFAULT { base_terrain GRAVEL_WET terrain_mask 1 height_limits 1 5 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain GRAVEL_ROAD { base_terrain GRAVEL_WET terrain_mask 1 height_limits 0 1 land_percent 100 number_of_clumps 9320 set_scale_by_groups }
create_terrain DLC_NEWSHALLOW { base_terrain GRAVEL_WET terrain_mask 0 height_limits 0 0 set_flat_terrain_only spacing_to_other_terrain_types 1 land_percent 100 number_of_clumps 9320 set_scale_by_groups }

create_terrain OAK_BUSH_FOREST { base_terrain PINE_FOREST  height_limits 0 0 land_percent 100 number_of_clumps 9320 set_scale_by_groups }


/* =================================================================================================== */
<CONNECTION_GENERATION>

/* custon beaches */
create_connect_all_lands {
	replace_terrain BEACH DLC_GRAVELBEACH
	terrain_size DLC_DRYGRASS 960 0
}	

/* must be the last connection */
create_connect_to_nonplayer_land {
	replace_terrain PINE_FOREST LEAVES

	terrain_size PINE_FOREST 8 0
	terrain_size GRASS 8 0
	terrain_size GRASS3 8 0
	terrain_size GRAVEL_WET 8 0
}


/* =================================================================================================== */
<OBJECTS_GENERATION>


		#const ESCALATION_REVEALER 1775
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_LINE_OF_SIGHT 20
		effect_amount SET_ATTRIBUTE ESCALATION_REVEALER ATTR_HITPOINTS 0

				create_object ESCALATION_REVEALER
				{
					set_place_for_every_player
					min_distance_to_players 0
					max_distance_to_players 0
				}
create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}

		/* Placeholder object with no terrain restrictions. */
		#const PH_GENERIC_ON 600
		#const PH_GENERIC_OFF 649
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_ON ATTR_STORAGE_VALUE 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_TERRAIN_ID 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_GENERIC_OFF ATTR_STORAGE_VALUE 0
	
		/* Placeholder object that may only generate on land terrains. */
		#const PH_LAND_ON 601
		#const PH_LAND_OFF 643
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_TERRAIN_ID 8
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_LAND_ON ATTR_STORAGE_VALUE 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_DEAD_ID -1
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_TERRAIN_ID 8
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_LINE_OF_SIGHT 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_HITPOINTS 0
		effect_amount SET_ATTRIBUTE PH_LAND_OFF ATTR_STORAGE_VALUE 0
	
		#const LAZY_MALE 1285 /* FE Flag */
		effect_amount SET_ATTRIBUTE LAZY_MALE ATTR_DEAD_ID 83
		effect_amount SET_ATTRIBUTE LAZY_MALE ATTR_HITPOINTS 0
	
		#const LAZY_FEMALE 851 /* ES Flag */
		effect_amount SET_ATTRIBUTE LAZY_FEMALE ATTR_DEAD_ID 293
		effect_amount SET_ATTRIBUTE LAZY_FEMALE ATTR_HITPOINTS 0

			
				create_object PH_GENERIC_OFF
				{
					set_place_for_every_player
					set_gaia_object_only
					min_distance_to_players 0
					max_distance_to_players 0
			
					actor_area 6001
					actor_area_radius 1
				}

					create_object PH_GENERIC_OFF /* Near Positioner */
					{
						set_place_for_every_player
						set_gaia_object_only
						number_of_objects 5
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 5100
						actor_area_radius 0

						if DEBUG_MODE
							second_object FLAG_A
						endif
					}

					create_object PH_GENERIC_OFF /* Far Positioner */
					{
						set_place_for_every_player
						set_gaia_object_only
						number_of_objects 5
						max_distance_to_players 2
						temp_min_distance_group_placement 1
						find_closest

						actor_area 5101
						actor_area_radius 1

						avoid_actor_area 6001 /* Minimum Player Distance */

						if DEBUG_MODE
							second_object FLAG_B
						endif
					}

					create_object PH_GENERIC_OFF /* Villager Avoider */
					{
						set_place_for_every_player
						set_gaia_object_only
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 5102
						actor_area_radius 0

						avoid_actor_area 5100 /* Near Positioner */

						if DEBUG_MODE
							second_object FLAG_C
						endif
					}

					create_object PH_GENERIC_OFF /* Blocking Seventh Villager */
					{
						set_place_for_every_player
						set_gaia_object_only
						max_distance_to_players 2
						find_closest

						actor_area 5103
						actor_area_radius 0

						avoid_actor_area 5101 /* Far Positioner */
						avoid_actor_area 6001 /* Minimum Player Distance */

						if DEBUG_MODE
							second_object FLAG_D
						endif
					}

					if CENTRALISE_OBJECT
						create_object PH_GENERIC_ON
					else
						create_object PH_GENERIC_OFF
					endif
					{
						second_object DLC_COW
						set_place_for_every_player
						min_distance_to_players 1
						max_distance_to_players 1
						find_closest

						actor_area 2000
						actor_area_radius 1

						avoid_actor_area 5100 /* Herdable Positioner */
					}

				start_random
				  percent_chance 50 #define LM_A
				  percent_chance 50 #define LF_A
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_A
				   create_object LAZY_MALE
				  elseif LF_A
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_A
				     second_object LAZY_MALE
				    elseif LF_A
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_B
				  percent_chance 50 #define LF_B
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_B
				   create_object LAZY_MALE
				  elseif LF_B
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_B
				     second_object LAZY_MALE
				    elseif LF_B
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_C
				  percent_chance 50 #define LF_C
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_C
				   create_object LAZY_MALE
				  elseif LF_C
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_C
				     second_object LAZY_MALE
				    elseif LF_C
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_D
				  percent_chance 50 #define LF_D
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_D
				   create_object LAZY_MALE
				  elseif LF_D
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_D
				     second_object LAZY_MALE
				    elseif LF_D
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_E
				  percent_chance 50 #define LF_E
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_E
				   create_object LAZY_MALE
				  elseif LF_E
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_E
				     second_object LAZY_MALE
				    elseif LF_E
				      second_object LAZY_FEMALE
				    endif
				  endif
				}
				
				start_random
				  percent_chance 50 #define LM_F
				  percent_chance 50 #define LF_F
				end_random
				
				if CENTRALISE_OBJECT
				  if LM_F
				   create_object LAZY_MALE
				  elseif LF_F
				   create_object LAZY_FEMALE
				  endif
				else
				  create_object PH_GENERIC_OFF
				endif
				{
				  set_place_for_every_player
				  min_distance_to_players 1
				  actor_area_to_place_in 2000 /* EW Herdable */
				
				  actor_area 1000
				  actor_area_radius 0
				
				  avoid_actor_area 1000 /* Villager */
				  avoid_actor_area 5102 /* Villager Avoider */
				  avoid_actor_area 5103 /* Blocking Seventh Villager */
				
				  if CENTRALISE_OBJECT
				  else
				    if LM_F
				     second_object LAZY_MALE
				    elseif LF_F
				      second_object LAZY_FEMALE
				    endif
				  endif
				}

create_object DLC_DEADTREE
{ 
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 5
	max_distance_to_players 6
	set_circular_placement
	enable_tile_shuffling
	avoid_forest_zone 2
	avoid_actor_area 9
	number_of_objects 1
	actor_area 5423
	actor_area_radius 1
}

create_object VILLAGER
{
	set_place_for_every_player
	number_of_objects 3
	actor_area_to_place_in 5423
}

create_object HOUSE 
{
	number_of_objects 2
	temp_min_distance_group_placement 6
	find_closest
	set_place_for_every_player
	min_distance_to_players 8
}


create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		max_distance_to_players 11
		find_closest_to_map_edge
		set_circular_placement
		min_distance_group_placement 2
	}
endif


/* center */

create_object DLC_BEAR {
	number_of_groups 6
	set_scaling_to_map_size
	place_on_specific_land_id 1
	temp_min_distance_group_placement 3
	terrain_to_place_on DLC_NEWSHALLOW
	second_object SHORE_FISH
}

create_object STONE {
	number_of_objects 6
	group_placement_radius 2
	number_of_groups 9320
	set_tight_grouping
	set_scaling_to_map_size
	place_on_specific_land_id 1
	find_closest
	temp_min_distance_group_placement 8
	min_distance_group_placement 4
	terrain_to_place_on GRAVEL_WET
	ignore_terrain_restrictions
}


/* default straggler trees */
if DEATH_MATCH
elseif DEFEND_WONDER
elseif EMPIRE_WARS
else
create_object DLC_DEADTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	set_circular_placement
	min_distance_group_placement 1
	set_facet rnd(15,20)
}
create_object DLC_DEADTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
	set_facet rnd(15,20)
}
endif

/* near forage */
create_object FORAGE
{
	number_of_objects 6
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	max_distance_to_players 17
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 8
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	terrain_to_place_on DLC_DRYGRASS
}

/* near gold */
create_object GOLD
{
	number_of_objects 7
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	max_distance_to_players 17
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 8
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	terrain_to_place_on DLC_DRYGRASS
}

/* medium gold */
create_object GOLD
{
	number_of_objects 4
	number_of_groups 2
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	find_closest
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 20
	avoid_forest_zone 2
	actor_area 10
	actor_area_radius 8
	avoid_actor_area 10
	terrain_to_place_on DLC_DRYGRASS
}

create_object DLC_COW
{
	number_of_objects 2
	set_loose_grouping
	set_place_for_every_player
	min_distance_to_players 8
	find_closest
	set_circular_placement
	avoid_forest_zone 1
}

create_object BOAR
{	
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_to_players 14
	find_closest
	set_circular_placement
	temp_min_distance_group_placement 8
}

create_object SHEEP
{	
	number_of_objects 2
	number_of_groups 1
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 17
	find_closest
	set_circular_placement
	avoid_forest_zone 2
	temp_min_distance_group_placement 18
}



/* scattered resources */

/* relic balance from DE */
create_object RELIC
{
	max_distance_to_other_zones 2
	avoid_forest_zone 1
	min_distance_to_players 25
	set_circular_placement
	min_distance_group_placement 1
	if TINY_MAP
		number_of_objects            5
		temp_min_distance_group_placement 35
	elseif SMALL_MAP
		number_of_objects            5
		temp_min_distance_group_placement 38
	elseif MEDIUM_MAP
		number_of_objects            5
		temp_min_distance_group_placement 38
	elseif LARGE_MAP
		number_of_objects            7
		temp_min_distance_group_placement 48
	elseif HUGE_MAP
		number_of_objects            8
		temp_min_distance_group_placement 52
	else
		number_of_objects            999
		temp_min_distance_group_placement 52 
	endif
}

create_object STONE_ROCK {
    number_of_groups 9320
	number_of_objects 1
    set_gaia_object_only
	min_distance_to_players 35
	set_circular_placement
	temp_min_distance_group_placement 25
	terrain_to_place_on DLC_DRYGRASS
	layer_to_place_on GRAVEL_DEFAULT
	actor_area 839
	actor_area_radius 1
}
create_object STONE {
	number_of_groups 9320
	set_gaia_object_only
	min_distance_group_placement 1
	temp_min_distance_group_placement 1
	actor_area_to_place_in 839
}

/* extra gold */
create_object GOLD {
    number_of_groups 9320
	number_of_objects 3
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_to_players 30
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 25
	terrain_to_place_on DLC_DRYGRASS
	layer_to_place_on GRAVEL_DEFAULT
}

create_object DEER {
	number_of_groups 9320
	number_of_objects 2
	set_gaia_object_only
	min_distance_group_placement 4
	temp_min_distance_group_placement 18
	min_distance_to_players 40
	set_circular_placement
	terrain_to_place_on GRASS2
}

create_object WOLF {
	number_of_groups 9320
	number_of_objects 1
	set_gaia_object_only
	min_distance_group_placement 4
	temp_min_distance_group_placement 32
	min_distance_to_players 55
	set_circular_placement
	terrain_to_place_on GRASS2
}

create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 15
}
create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 16
}
create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 17
}
create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 18	
}
create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 19
}
create_object DLC_DEADTREE {
	number_of_groups 5
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 1
	avoid_forest_zone 1
	temp_min_distance_group_placement 16
	min_distance_to_players 20
	set_circular_placement
	set_facet 20
}

/* birds */
create_object HAWK {
	number_of_objects 2
	set_scaling_to_map_size
}
create_object DLC_VULTURE {
	number_of_objects 2
	set_scaling_to_map_size
}
create_object FALCON {
	number_of_objects 2
	set_scaling_to_map_size
}